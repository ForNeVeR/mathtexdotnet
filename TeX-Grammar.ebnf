input = fractional expr , ? EOF ? ;
fractional expr = expr [ "\over" , expr ] ;
expr = term , { ( "+" | "-" ) , term } ;
term = signed value , [ ( "*" | "/" | "\times" | "\cdot" | "\bmod" ) , term | implicit term ] ;
implicit term = factorial value , implicit term | term ;
signed value = [ "+" | "-" ] , factorial value ;
factorial value = indexed value , [ "!" ] ;
indexed value = value , indices pair ;
value = raw value | group | bracket expr | factorial | fraction | binomial | root | function | big op | text ;
raw value = ? number ? | ? letter ? | ? greek letter ? ;
indices pair = ( [ "^" , index ] , [ "_" , index ] ) | ( [ "_" , index ] , [ "^" , index ] ) ;
index = raw value | group ;
group = "{" , expr , "}" ;
bracket expr = "{" , expr , "}" | "(" , expr , ")" | "[" , expr , "]" | "\{" , expr , "\}" ;
fraction = "\frac" , group , group ;
binomial = "\binom" , group , group ;
root = "\sqrt" , [ argument ] , group ;
function = ? function name ? , indices pair , expr ;
argument = "[" , expr , "]" ;
big op = ? big op ? , indices pair , expr ;
text =  "{" , { ? letter ? }- , "}" ;
